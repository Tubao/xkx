#alias auto_lianwu
{
	#list worklist create 
		{xx_start};
	worklist_start;
};
#alias cai_watch
{
	#var sb_or_zi %1;
	#class {c_cai_watch} open;
	#alias caijy
	{
		#map run 6;
		drink;
		#3 eat fruit;
		#delay {1}
		{
			#map run 12;
			#ticker {t_caijy}
			{
				cai jiaoyin;
			}{2};
		};
	};
	#act {^你头昏脑胀，完全地上记不得步法}
	{
		#untick {t_caijy};
		#delay {2}
		{
			#map run 1;
			#format work_done_flag %T;
		};
	};
	#alias watchsb
	{
		#map run 10;
		#ticker {t_watchsb}
		{
			#if {$sb_or_zi==0}
			{
				watch 石壁;
			}
			{
				watch zi;
			};
		}{2};
	};
	#act {^你精神不够集中，无法领悟石壁}
	{
		yun regenerate;		
	};
	#act {^你精神不够集中，无法学习石壁上的字}
	{
		yun regenerate;		
	};
	#act {^你的内力不够} 
	{
		#untick {t_watchsb};
		#delay {2}
		{
			#map run 1;	
			#format work_done_flag %T;
			enter;s;sleep;					
		};
	};
	#act {^你一觉醒来}
	{
		n;out;worklist_start;	
	};
	#act {^你刚在三分钟内睡过一觉}
	{
		#delay {30}
		{
			n;out;worklist_start;
		};
	};
	#class {c_cai_watch} close;
	#list worklist create 
		{caijy}
		{watchsb};
	worklist_start;	
};
#alias xx_q_lianwu
{
	#class {c_xx_q_lianwu} open;
	#alias caijy
	{
		#map run 6;
		drink;
		#3 eat fruit;
		#delay {1}
		{
			#map run 12;
			#ticker {t_caijy}
			{
				cai jiaoyin;
			}{2};
		};
	};
	#act {^你头昏脑胀，完全地上记不得步法}
	{
		#untick {t_caijy};
		#delay {2}
		{
			#map run 1;
			#format work_done_flag %T;
		};
	};
	#alias watchsb
	{
		#map run 10;
		#ticker {t_watchsb}
		{
			watch 石壁;
		}{2};
	};
	#act {^你精神不够集中，无法领悟石壁}
	{
		yun regenerate;		
	};
	#act {^你的内力不够} 
	{
		#untick {t_watchsb};
		#delay {2}
		{
			#map run 1;	
			#format work_done_flag %T;
			#var is_doing_task 0;
		};
	};
	#class {c_xx_q_lianwu} close;
	#list worklist create 
		{caijy}
		{watchsb};
	worklist_start;	
};

#act {^葫芦已经被喝得一滴也}
{
	drop hulu;
};
#act {^也许是上天垂青于你的勤奋和努力吧}
{
	dazuo 15;
	#delay {3}
	{
		tuna 15;
	};
};

#alias xx_start
{	
	#class {c_lianwu_master} open;
	#class {c_xx_q_lianwu} kill;
	loadmap xx_new;
	#list {xx_skills} {create} {force}{parry}{strike}{huagong-dafa}{xingxiu-duzhang}{zhaixinggong}{tianshan-zhang};
	#list {xx_skills} size xx_size;
	#var maxlevel {{force}{50}{parry}{50}{strike}{50}{huagong-dafa}{80}{xingxiu-duzhang}{50}{zhaixinggong}{50}{tianshan-zhang}{76}};
	#var skill_ind 1;
	#ticker {t_hp} 
	{
		hp;
		#delay {2}
		{
			#showme {status 精神: $mystatus[js_1]/$mystatus[js_2] ; 气血: $mystatus[qx_1]/$mystatus[qx_2] ;内力:$mystatus[nl_1]; 食物: $mystatus[sw_1] ; 饮水:  $mystatus[ys_1] ; 状态: $mystatus[zt]};		
		};	
	}{60};	
	#alias xx_zhaiguo
	{
		#map run 18;
		#list zhaiguo_list create
		{zhai}
		{w;zhai}
		{n;zhai}
		{e;zhai}
		{s};
		#var zhaiguo_ind 1;
		#list zhaiguo_list size zhaiguo_size;
		#ticker {t_zhaiguo}
		{
			#if {$zhaiguo_ind>$zhaiguo_size}
			{
				#untick {t_zhaiguo};
				#delay {2}
				{
					#map goto 18;
					#map run 1;
				};				
			}
			{
				$zhaiguo_list[$zhaiguo_ind];
				#math zhaiguo_ind $zhaiguo_ind+1;
			};
		}{2};
	};		
	#act {^你今天太累了，结果什么也没有学到。} 
	{
		yun regenerate;					
	};
	#act {^你的内力不够} 
	{
		setHeartbeat;
		save;
		s;
		drink;
		n;
		eat fruit;		
		enter;s;sleep;
	};	
	#act {^你一觉醒来，精神抖擞地活动了几下手脚}
	{
		#delay {1}
		{
			n;out;
		};
	};
	#act {^你刚在三分钟内睡过一觉}
	{
		#math rr 1d8;
		#if {$rr==1}
		{
			#map run 12;
			#ticker {t_caijy}
			{
				cai jiaoyin;
			}{2};
		}
		{
			xx_zhaiguo;
		};		
	};
	#act {^你头昏脑胀，完全地上记不得步法}
	{
		#untick {t_caijy};
		#delay {4}
		{
			#math r 1d8;
			#if {$r==1}
			{
				xx_zhaiguo;
			}
			{
				#map run 1;
			};			
		};
	};	
	#ticker {t_xx}
	{
		#var curr_skill $xx_skills[$skill_ind];
		#if {$myskill[$curr_skill]>=$maxlevel[$curr_skill]}
		{
			#math skill_ind $skill_ind+1;
			#if {$skill_ind>$xx_size}
			{
				xx_stop;
			};	
		};
		#ALIAS xx {xue dizi for $xx_skills[$skill_ind] 10};
		xx;			
	}{2};
	#ticker {t_sk} 
	{
		sk;		
	}{30};	
	#class 	{c_lianwu_master} close;
};
#alias xx_stop
{
	#untick {t_xx};
	#class 	{c_lianwu_master} kill;
	#format work_done_flag %T;
};
#alias dazuo_start
{	
	#var dazuo_num %1;
	#class {c_dazuo} open;
	#var maxnl {990};
	#act {^你吐纳完毕，睁开双眼，站了起来}
	{
		dazuo $dazuo_num;
	};
	#act {^你运功完毕，深深吸了口气}
	{
		dazuo $dazuo_num;
	};
	#act {^你现在的气太少了，无法产生内息运行全身经脉}
	{
		save;
		eat gan liang;
		drink hulu;
		sleep;
	};
	#act {^你一觉醒来，精神抖擞地活动了几下手脚} 
	{
		tuna $dazuo_num;
	};
	#act {^你现在身体状况太差了}
	{
		sleep;
	};	
	#ticker {t_dazuo_tuna}
	{
		#if {$mystatus[nl_2]<$maxnl}
		{
			dazuo $dazuo_num;
		}
		{
			dazuo_stop;
		};
		
	}{15};
	#class {c_dazuo} close;
	dazuo $dazuo_num;
};
#alias dazuo_stop
{
	#untick {t_dazuo_tuna};
	#class {c_dazuo} kill;
	#format work_done_flag %T;
};
#alias tuna_start
{
	#var tuna_num %1;
	#class {c_tuna} open;	
	#var maxjl {990};
	#act {^你吐纳完毕，睁开双眼，站了起来}
	{
		tuna $tuna_num;
	};
	#act {^你运功完毕，深深吸了口气}
	{
		tuna $tuna_num;
	};
	#act {^你现在精不足，无法修行精力}
	{		
		yun regenerate;				
	};
	#act {^你一觉醒来，精神抖擞地活动了几下手脚} 
	{
		dazuo 50;
	};	
	#act {^你的内力不够} 
	{
		save;
		eat gan liang;
		drink hulu;
		sleep;
	};	
	#ticker {t_dazuo_tuna}
	{
		#if {$mystatus[jl_2]<$maxjl}
		{
			tuna $tuna_num;
		}
		{
			tuna_stop;
		};		
	}{10};
	#class {c_tuna} close;
	tuna $tuna_num;
};
#alias tuna_stop
{
	#untick {t_dazuo_tuna};
	#class {c_tuna} kill;
	#format work_done_flag %T;
};
#alias readbook_start
{
	#class {c_ds} open;
	#list {rb_skills} {create} {force}{parry}{hero-jianfa}{sword}{blade};
	#list {rb_skills} size rb_size;
	#var maxlevel {{force}{94}{parry}{94}{hero-jianfa}{94}{sword}{94}{blade}{94}};	
	#var skill_book {{force}{read shediao %1}{parry}{read shediao %1}{hero-jianfa}{du hero book for %1}{sword}{du sword book for %1}{blade}{du blade book for %1}};	
	#alias ds {read shediao %1};
	#var skill_ind 1;	
	#act {^你已经很累了，歇歇再读吧}
	{
		yun regenerate;
	};
	#act {^你现在过于疲倦，无法专心下来研读新知}
	{
		yun regenerate;
	};
	#act {^你减缓真气运行，让自身恢复正常}
	{
		#untick {t_slowds};
		yun regenerate;
	};
	#act {^你略一凝神，精神看起来好多了}
	{
		ds 100;
	};
	#act {^你的内力不够} 
	{
		save;
		eat gan liang;
		drink hulu;
		yun jing;
		#ticker {t_slowds}
		{
			ds 2;
		}{2};
	};	
	#ticker {t_ds}
	{
		ds 100;
		#var curr_skill $rb_skills[$skill_ind];
		#if {$myskill[$curr_skill]>=$maxlevel[$curr_skill]}
		{
			#math skill_ind $skill_ind+1;
			#if {$skill_ind>$rb_size}
			{
				readbook_stop;
			}
			{
				#alias ds {$skill_book[$rb_skills[$skill_ind]]};
			};	
		};				
				
	}{60};	
	#class {c_ds} close;	
};
#alias readbook_stop
{
	#untick {t_ds};
	#untick {t_slowds};
	#class {c_ds} kill;
	#format work_done_flag %T;
};
#alias snow_start
{
	#class {c_snow} open;
	#var maxlevel {100};
	#act {^你突然发现在路旁的一片积雪上行走} {walk snow};
	#act {^你一路走下来，看着脚印} 
	{
		eat gan liang;
		drink hulu;
		save;
		look snow;
	};
	#act {^突然间，你被积雪闪耀着}
	{
		#delay {20}
		{
		
			look snow;
		};
	};
	#ticker {t_snow}
	{
		#if {$myskill[dodge]<$maxlevel}
		{
			look snow;
		}
		{
			snow_stop;
		};		
	}{5};
	#class {c_snow} close;
};
#alias snow_stop
{
	#untick {t_snow};
	#class {c_snow} kill;
	#format work_done_flag %T;
};
#alias lian_start
{
	#class {c_lx} open;
	#list {special_skill} {create} {tianshan-zhang} {xingxiu-duzhang} {zhaixinggong};
	#list {basic_skill} {create} {staff} {strike} {dodge};
	#list {basic_skill} {size} {skill_size};
	#var maxlevel {{tianshan-zhang}{90} {xingxiu-duzhang}{50} {zhaixinggong}{75}};
	#var s_index 1;
	#alias lx {lian staff %1};	
	#act {^你的体力不够}
	{
		yun recover;
		lx 1;
	};
	#act {^你现在气血虚浮}
	{
		yun recover;
		lx 1;
	};		
	#act {^你试着练习}
	{
		lx 1;
	};
	#act {^你的内力不够} 
	{
		save;
		eat Fruit;
		w;out;s;drink;
		n;enter;s;
		sleep;
		#var is_sleeping 1;
	};
	#act {^你现在内力不足} 
	{
		save;
		eat Fruit;
		w;out;s;drink;
		n;enter;s;
		sleep;
		#var is_sleeping 1;
	};
	#act {^你太累了，没有办法练习}
	{
		yun recover;
		lx 1;
	};
	#act {^你一觉醒来} 
	{
		n;e;
		#var is_sleeping 0;
	};	
	#act {^你刚在三分钟内睡过一觉}
	{
		#delay {10}
		{
			sleep;
		};
	};	
	#ticker {t_lx}
	{	
		#while {$s_index<=$skill_size}
		{
			#if {$myskill[$special_skill[$s_index]]>=$maxlevel[$special_skill[$s_index]]}
			{
				#math s_index {$s_index+1};
				#alias lx {lian $basic_skill[$s_index] %1};
				unwield all;
				#if {"$basic_skill[$s_index]"=="sword"}
				{
					wield sword;
				};
				#if {"$basic_skill[$s_index]"=="blade"}
				{
					wield blade;
				};
			}
			{
				#if {$is_sleeping==0}
				{
					lx 1;
				};
				#break;
			};
		};
		#if {$s_index>$skill_size}
		{
			lian_stop;			
		};				
	}{15};		
	#class {c_lx} close;	
};
#alias lian_stop
{	
	#untick {t_lx};	
	#class {c_lx} kill;
	#format work_done_flag %T;
};
