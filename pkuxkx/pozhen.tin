#split;
#config repeat on;



#alias ask.job {
	#var pz_goon 1;
	#class ask.job open;
	#action {^你对陆乘风道：“听闻近来有人冒名桃花岛弟子在%S的%S布下迷阵行恶，我愿代劳除去此獠。”} {
		#var pz_flag 1;
		#var jobarea %%1;
		#var jobplace %%2;
		zhanbu.place;			
	};
	#action {^陆乘风对着你点了点头。}
	{
		#delay {2}
		{
			#if {$pz_flag == 1}
			{
				#unticker {t_askpzjob};
				ngt $jobarea$jobplace;
				#var pz_flag 0;
				#act {^dest!}
				{
					#unact {^dest!};
					zhanbu.maze;
				};
			};
		};
		#class ask.job kill;	
	};
	
		
	#class ask.job close;
	#untick t_sk;	
	#var pz_flag 0;
	ask lu about 破阵;
};
#alias ask.over {ask lu about 破阵奖励};
#alias ask.fail {ask lu about 破阵失败};
#alias zhanbu.place {
	#echo <cfa><EAA>=================================================;
	#echo <cfa><EAA>　　破阵地点：<AAA>　$jobarea $jobplace 。;
	#echo <cfa><EAA>=================================================;
};
#alias zhanbu.maze {
	#class zhanbu.maze open;
	#var pos {{nw}{2}{n}{2}{ne}{2}{w}{2}{e}{2}{sw}{2}{s}{2}{se}{2}};
	#var posstep 0;
	#var direc {nw;n;ne;w;e;sw;s;se};
	#foreach $direc direction {
		#var exec {#alias $direction {#var pace $direction;#send $direction}};
		$exec;
	};
	#var flag 0;
	#function printdirec {
		#var printtext {　　经推算，正确方向可能为：};
		#list cdireclist create;
		#foreach $direc direction {
			#if {$pos.$direction} {
				#var printtext $printtext $direction;
				#list cdireclist add {$direction};
			};
		};
		#showme $printtext;		
		a2c hpbrief;
		#delay {2}
		{
			#if {$mystatus[qx_1]<350}
			{
				#var flag 1;
			}
			{
				#var flag 0;
			};
			#if {"$flag"=="0"}
			{
				$cdireclist[1];
			}
			{
				#foreach $direc direction 
				{
					#list cdireclist {find} {$direction} {findflag};
					#if {$findflag==0}
					{
						$direction;
						#break;
					};
				};
			
			};
		};
		
	};
	#action {~{\e\[5m\e\[42|\e\[42}%S{\e\[5m\e\[42|\e\[42}%S{\e\[5m\e\[42|\e\[42}} {
		#if {$posstep} {
			#var posstep 0;
			#if {"%%1" == "\e[42"} {#var pos.sw 0};
			#elseif {"%%1" == "\e[5m\e[42" && !$pos.sw} {#var pos.sw 0};
			#else {#var pos.sw 1};
			#if {"%%3" == "\e[42"} {#var pos.s 0};
			#elseif {"%%3" == "\e[5m\e[42" && !$pos.s} {#var pos.s 0};
			#else {#var pos.s 1};
			#if {"%%5" == "\e[42"} {#var pos.se 0};
			#elseif {"%%5" == "\e[5m\e[42" && !$pos.se} {#var pos.se 0};
			#else {#var pos.se 1};
			@printdirec{};
		};
		#else {
			#var posstep 1;
			#if {"%%1" == "\e[42"} {#var pos.nw 0};
			#elseif {"%%1" == "\e[5m\e[42" && !$pos.nw} {#var pos.nw 0};
			#else {#var pos.nw 1};
			#if {"%%3" == "\e[42"} {#var pos.n 0};
			#elseif {"%%3" == "\e[5m\e[42" && !$pos.n} {#var pos.n 0};
			#else {#var pos.n 1};
			#if {"%%5" == "\e[42"} {#var pos.ne 0};
			#elseif {"%%5" == "\e[5m\e[42" && !$pos.ne} {#var pos.ne 0};
			#else {#var pos.ne 1};
		};
	} {1};
	#action {~{\e\[5m\e\[42|\e\[42}%S{\e\[5m\e\[42|\e\[42}} {
		#if {"%%1" == "\e[42"} {#var pos.w 0};
		#elseif {"%%1" == "\e[5m\e[42" && !$pos.w} {#var pos.w 0};
		#else {#var pos.w 1};
		#if {"%%3" == "\e[42"} {#var pos.e 0};
		#elseif {"%%3" == "\e[5m\e[42" && !$pos.e} {#var pos.e 0};
		#else {#var pos.e 1};
	} {2};
	#action {你尝试破阵，可惜选择了错误的方向，损失了一些气血和内力。} {
		#var pos.$pace 0;		
	};
	#action {这里的迷阵在你眼中如同儿戏一般。} {
		#var pos {{nw}{2}{n}{2}{ne}{2}{w}{2}{e}{2}{sw}{2}{s}{2}{se}{2}};
		#var posstep 0;
	};
	#action {破阵失败！这里的真相恐怕永远被掩盖。} {
		
		gtpz;
		#act {^dest!}
		{
			#unact {^dest!};
			ask.fail;
			gf bosss;
			#act {^本神说道：「下一位」}
			{
				#unact {^本神说道：「下一位」};
				yun recover;
				#delay {1}
				{
					dazuo max;
				};
				#act {^你正在运行内功加速全身气血恢复，无法静下心来搬运真气。}
				{
					#unact {^你正在运行内功加速全身气血恢复，无法静下心来搬运真气。};
					#act {^你减缓真气运行，让气血运行恢复正常。}
					{
						#unact {^你减缓真气运行，让气血运行恢复正常。};						
						#delay {1}
						{
							yun recover;
							#delay {1}
							{
								dazuo max;
							};
						};
					};
				};
			};
		};
	};
	#action {你彻底破解了这里的迷阵！} {
		
		#echo <cfa><EAA>=================================================;
		#echo <cfa>　　破阵成功！;
		#echo <cfa><EAA>=================================================;
	};
	#action {看起来%S想杀死你！} {
		#class zhanbu.maze.kill open;
		#action {大 恶 %%1(%D)} {
			#format npcid %l %%%1;
			kill $npcid;			
		};
		#action {%%1道：想不到我最终还是失败了！哎！} {			
			#class zhanbu.maze.kill kill;
			#echo <cfa><EAA>=================================================;
			#echo <cfa>　　破阵任务成功完成！;
			#echo <cfa><EAA>=================================================;
			s;
			gtpz;
			#act {^dest!}
			{
				#unact {^dest!};
				ask.over;
				gf bosss;
				#act {^本神说道：「下一位」}
				{
					#unact {^本神说道：「下一位」};
					yun recover;
					#delay {1}
					{
						dazuo max;
					};
					
					#act {^你正在运行内功加速全身气血恢复，无法静下心来搬运真气。}
					{
						#unact {^你正在运行内功加速全身气血恢复，无法静下心来搬运真气。};
						#act {^你减缓真气运行，让气血运行恢复正常。}
						{
							#unact {^你减缓真气运行，让气血运行恢复正常。};						
							#delay {1}
							{
								yun recover;
								#delay {1}
								{
									dazuo max;
								};
							};
						};
					};
				};
			};
		};
		#class zhanbu.maze.kill close;
	};
	#act {^你运功完毕，深深吸了口气，站了起来。}
	{
		#class zhanbu.maze kill;
		#unact {^你运功完毕，深深吸了口气，站了起来。};
		yun recover;
		#if {$pz_goon==1}
		{
			#ticker {t_askpzjob}
			{
				ask.job;
			}{15};
		};
	};
	#class zhanbu.maze close;
	jiali max;
	yun recover;
	yun regenerate;
	yun qi;
	yun powerup;
	zhanbu -maze;
};
#alias stop_pz
{
	#var pz_goon 0;
};

