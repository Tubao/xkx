#var Nickname {伯特子};
#var mydummy {xarnold};
#var mytcid {xxkfk};
#alias gttc {hgt ee1388ecd95ac786ac135053d308fc78};
#var tc_count 1;
#alias start_tc
{	
	jiali 0;
	special + sociability;	
	#untick t_sk;	
	#class tuiche open;
	#ALIAS ge {gan che to east};
	#ALIAS genter {gan che to enter};
	#ALIAS geu {gan che to eastup};
	#ALIAS gn {gan che to north};
	#ALIAS gne {gan che to northeast};
	#ALIAS gnu {gan che to northup};
	#ALIAS gnw {gan che to northwest};
	#ALIAS gout {gan che to out};
	#ALIAS gs {gan che to south};
	#ALIAS gse {gan che to southeast};
	#ALIAS gsu {gan che to southup};
	#ALIAS gsw {gan che to southwest};
	#ALIAS gw {gan che to west};
	#ALIAS gwu {gan che to westup};
	#ALIAS ged {gan che to eastdown};
	#ALIAS gnd {gan che to northdown};
	#ALIAS gwd {gan che to westdown};
	#ALIAS gsd {gan che to southdown};
	#ALIAS gu {gan che to up};
	#ALIAS gd {gan che to down};
	#var start_rid {ee1388ecd95ac786ac135053d308fc78};
	#var tc_flag 0;	
	#alias tc_reset
	{
		ask lin about 重置任务;
		#var tc_count 1;
	};	
	#act {%* %* %* %*秒%*待认领%*0}
	{
		getesc %%1;	
		#var tc_flag 1;	
		#untick t_listesc;
	};
	#alias asktc.job
	{
		#showme {推车第$tc_count轮};		
		#nop ask lin zhennan about 护镖;
		listesc;	
		#var ref_rid $start_rid;
		#var checkloc 0;		
		#var tc_finished 0;
		#ticker t_listesc
		{
			listesc;
		}{3};			
	};
	#alias asktc.over
	{
		#unact {劫匪突然从暗处跳了出来，阴笑道：“红货和人命都留下来吧！。”};
		ask lin zhennan about finish;
		#if {$tc_count>24}
		{
			tc_reset;
		};
		#var tc_flag 0;	
		#delay {2}
		{
			drop shi tan;
			drop xuan bing;
			#if {$needqx==1}
			{
				#var needqx 0;
				yun heal;
				#delay {1}
				{
					yun heal;
				};
			};		
		};
		#tick t_tcdz
		{
			dazuo max;
		}{5};
		#class tcover open;		
		#act {^你运功完毕，深深吸了口气，站了起来。}
		{
			#untick t_tcdz;
			#class tcover kill;
			yun recover;
			asktc.job;
		};
		#class tcover close;
	};
		
	#alias asktc.fail
	{
		#unact {劫匪突然从暗处跳了出来，阴笑道：“红货和人命都留下来吧！。”};
		ask lin zhennan about fail;
		#var tc_flag 0;	
		#var tc_count 0;
	};
	#alias move_next
	{
		#math tc_step_count $tc_step_count+1;
		#if {$tc_step_count==5}
		{
			#var tc_step_count 0;
			#var checkloc 1;
		};
		#if {$checkloc==1}
		{
			#var checkloc 0;
			a2c look;
			#delay {1.5}
			{
				#list inner_Route_list create;
				#script {ret} {python ./tintin/myscripts/pkuxkx/gps/getcmds_sbs.py "$ref_rid" "$lines" "$destrid"};
				#list ret size retsize;
				#if {$retsize>0 && "$ret[1]"!="0"}
				{	
					#var ref_rid $ret[1];			
					#list ret delete {1};	
					#foreach {$ret[%*]} {cmd} {#list inner_Route_list add {$cmd};};			
					#list inner_Route_list add {#showme {hasmoved}};
					#nop showme $inner_Route_list;
					inner_walk;
				}
				{
					#var checkloc 1;
					#var tc_step_count -1;
					move_next;
				};
			};
		}
		{
			#list inner_Route_list create;
			#script {ret} {python ./tintin/myscripts/pkuxkx/gps/getcmds_sbs2.py "$ref_rid" "$destrid"};
			#list ret size retsize;
			#if {$retsize>0 && "$ret[1]"!="0"}
			{	
				#var ref_rid $ret[1];			
				#list ret delete {1};	
				#foreach {$ret[%*]} {cmd} {#list inner_Route_list add {$cmd};};			
				#list inner_Route_list add {#showme {hasmoved}};
				#nop showme $inner_Route_list;
				inner_walk;
			}
			{
				#math tc_step_count $tc_step_count-1;
				#var checkloc 1;
				move_next;
			};
		};
	};
	#act {^hasmoved}
	{
		#showme {推车：第$tc_count轮};
		yun qi;		
		#delay {1.5} 
		{
			#ticker t_shou
			{
				shou $mytcid's robber;
			}{1.5};
		};
	};
	#act {^你想收谁作弟子？}
	{
		#untick t_shou;
		#untick t_pf;		
		#delay {1}
		{
			#if {$tc_finished==1}
			{
				hgt $start_rid;
				#act {^dest!}
				{
					#unact {^dest!};
					asktc.over;
				};
			}
			{
				#if {$neednl==1}
				{
					#var neednl 0;
					#tick t_tcdz
					{
						dazuo max;
					}{5};
					#class tcdz open;					
					#act {^你运功完毕，深深吸了口气，站了起来。}
					{
						#untick t_tcdz;
						#class tcdz kill;
						yun recover;
						move_next;
					};
					#class tcdz close;
				}
				{
					move_next;
				};
				#nop #showme {can move_next};
			};
		};
	};
	#alias tracehj
	{
		#var tra {tc.t $localdesc};
	    	#var traversal_handler {stopTbyNpc_cn $targetNpc};
	    	#echo {$tra};
	    	$tra;
	};
	#alias tell_dummy
	{
		#act {劫匪突然从暗处跳了出来，阴笑道：“红货和人命都留下来吧！。”}
		{
			#delay {2}
			{
				#ticker t_pf
				{
					pfh;			
				}{0.2};
			};
		};
		tell $mydummy xkxtc $localdesc $destrid $targetNpc;
		#var tc_step_count 0;
		#delay {6}
		{
			move_next;
		};
	};
	#act {^%*告诉你：xkxtc %* %* %*}
	{
		#untick t_hpbrief;
		#untick t_tellloc;
		#unact {劫匪突然从暗处跳了出来，阴笑道：“红货和人命都留下来吧！。”};
		#var localdesc  {%%2};
		#var destrid {%%3};
	    	#var targetNpc {%%4};
	    	tracehj;
	};
	#act {%*tcfinish}
	{
		#untick t_tellloc;
	};
	#act {告诉你：hjloc %*}
	{
		#if {"%%1"!=""}		
		{
			#var destrid  {%%1};	
		};	    	
	};
	#act {^你累了个半死，终于把镖运到了地头。} 
	{
		#math tc_count $tc_count+1;		
		#var tc_finished 1;
		tell $mydummy tcfinish;
	};
	#act {劫匪趁你不注意，推着镖车就跑，你赶紧追了上去。}
	{
		#showme {乱入};
		#var checkloc 1;
		#var tc_step_count 0;
	};
	#ACTION {林震南递给你一封密信。} {stop_tc;s; s; s; xian xin};
	#action {$Nickname把这批红货送到泉州当铺王福贵那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {泉州%%2};
	    #var destrid {888028bf893204bf96b95048b2ea46bb};
	    #var targetNpc {%%1};
	    tell_dummy;
	};
	#action {$Nickname把这批红货送到嘉兴陆家庄大厅陆立鼎那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {嘉兴%%2};
	    #var destrid {d690776f215ec9e60b7314839f1365cc};
	     #var targetNpc {%%1};
	     tell_dummy;
	};
	#ACTION {$Nickname把这批红货送到江州天福镖局王小月那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {江州%%2};
	    #var destrid {b54a9096e5698168dbc963f7ea946277};
	     #var targetNpc {%%1};
	     tell_dummy;
	};
	#ACTION {$Nickname把这批红货送到归云庄太湖街商人那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {归云庄%%2};
	    #var destrid {c3cb4bd21c43da4f6c3d48fbc4f263e7};
	     #var targetNpc {%%1};
	     tell_dummy;
	};
	#ACTION {$Nickname把这批红货送到岳王墓墓前广场包打听那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {岳王墓%%2};
	    #var destrid {aeb118fe2bed4e18dc074063b8351edd};
	     #var targetNpc {%%1};
	     tell_dummy;
	};
	#ACTION {$Nickname把这批红货送到牙山小荒地黄毛鬼那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {牙山%%2};
	    #var destrid {dc791906fd40a37ef42435e3c972862c};
	     #var targetNpc {%%1};
	     tell_dummy;
	};
	#ACTION {$Nickname把这批红货送到临安府江南钱庄金乞儿那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {临安府%%2};
	    #var destrid {e774007ac7be8f669693fac10a97364d};
	     #var targetNpc {%%1};
	     tell_dummy;
	};
	#ACTION {$Nickname把这批红货送到苏州聚宝斋孙剥皮那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {苏州%%2};
	    #var destrid {478df74cce2658601f36bdcd8bdb1b37};
	     #var targetNpc {%%1};
	     tell_dummy;
	};
	#ACTION {$Nickname把这批红货送到镇江飞龙镖局张经那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {镇江%%2};
	    #var destrid {978dd91452cfa9ca80d8816822786374};
	     #var targetNpc {%%1};
	     tell_dummy;
	};
	#ACTION {$Nickname把这批红货送到南昌飞虎镖局楚大业那里，他已经派了个伙计名叫%*到%*附近接你} {
	     #var localdesc  {南昌%%2};
	     #var destrid {0c8c3797aa6224ed192803d4f5c68939};
	      #var targetNpc {%%1};
	      tell_dummy;
	};
	#ACTION {$Nickname把这批红货送到嘉兴钱庄包方圆那里，他已经派了个伙计名叫%*到%*附近接你} {
	    #var localdesc  {嘉兴%%2};
	    #var destrid {d1013449893c5a09661043ef64bfe3bc};
	     #var targetNpc {%%1};
	     tell_dummy;
	};
	#act {从劫匪身上掉了出来%*}
	{
		get all;
	};
	#act {你捡起一件*} {drop cloth;drop pao;drop she pi};
	#act {你捡起一双*} {drop xie;drop shoes;};
	#act {你捡起一柄*} {drop changjian};
	#act {你捡起一条*} {drop she rou;drop tu rou;drop band};
	#act {你捡起一块*} {drop gan liang;drop tu rou};
	#act {^你决定开始跟随}
	{
		#act {^$targetNpc向%*离开。}
		{
			#untick t_tellloc;
			tracehj;
		};
		#act {^这里没有%*}
		{
			#untick t_tellloc;
			tracehj;
		};
		#tick t_tellloc
		{
			a2c look;
			#delay {2}
			{		
				#script {ret} {python ./tintin/myscripts/pkuxkx/gps/locatebylines.py "$destrid" "$lines"};
				#if {"$ret[1]"!="0"}
				{
					#var destrid $ret[1];	
					tell $mytcid hjloc $ret[1];
				};				
			};		
		}{10};
		
	};		
	
	#act {劫匪仓皇之际扔下了一些物品。}
	{
		#delay{0.5}
		{
			get all;
		};
	};
	#act {^你手上持有的武器并不能用来发动%*}
	{
		wield yuzhu zhang;
		wield zhang;
		wield staff;
	};
	#alias tx.ft
	{		
		#class c_txtri read ./tintin/myscripts/pkuxkx/auto_tx.tin;
		bei none;
		jiali 0;
		yun qi;
		fight $npcename;		
	};
	#class tuiche close;
};
#alias tc_resume
{
	#var ref_rid $start_rid;
	#var checkloc 0;
	#var tc_flag 1;	
	#var tc_finished 0;
	#var tc_step_count 4;
	move_next;
};
#alias stop_tc
{	
	#var traversal_handler {};
	#class tuiche kill;
	#class tcover kill;
	#untick t_tcdz;
	#untick t_pf;
};
#alias tc.t
{
	#var r_traversal {traversal %1};	
	#var hererid {bffdafab4df6cdbe66c1689dcdfa0f55};
	#var locdesc %1;
	#var depth %2;
	#if {$depth==""}
	{
		#var depth 8;
	};
	#var traversal_flag 0;
	#script {ret} {python ./tintin/myscripts/pkuxkx/gps/getRidsByName.py "$locdesc"};
	#list ret {size} {retsize};	
	#if {"$retsize"=="0"}
	{
		#showme {can not find dest location!};
	}
	{
		#var torid $ret[1];
		#math hgtcount 1;
		igt $hererid $torid;	
	
		#act {^dest!}
		{
			#unact {^dest!};
			#showme {stopt to stop traversal.};
			$traversal_handler;
			#delay {1}
			{
				look;			
				#delay {1}
				{
					#if {$traversal_flag == 0}
					{	
						#script {ret} {python ./tintin/myscripts/pkuxkx/gps/getcmds_traversal.py "$locdesc" "$depth"};
						#list inner_Route_list create;
						#list inner_Route_list add {#showme {traversal_start};};
						#list inner_Route_list add {setSlowWalk2;};
						#foreach {$ret[%*]} {cmd} {#list inner_Route_list add {$cmd};};	
						#list inner_Route_list add {#showme {traversal_finished};#var traversal_flag 1;};
						#list inner_Route_list add {setNormalWalk;};					
						inner_walk;
	
					};
				};
			};
		};
		#act {^traversal_finished}
		{
			#unact {^traversal_finished};
			r.tc.t 15;
		};		
	};
};
#alias r.tc.t
{
	$r_traversal %1;	
};
#alias set_tc
{
	#var tc_count %1;
};
#alias tcmx
{
	#class c_tcmx open;
	#act {卷走林震南财物的伙计 %*(%*)}
	{
		#format huoji %l {%%2};
		#5 ask $huoji about treasure;
	};
	#act {%*把一包财物砸向你，一转眼不见了。}
	{
		#class c_tcmx kill;
		set_destEventHandler tcmx.over;
		gttc;
	};
	#class c_tcmx close;
	set_destEventHandler tcmx.zhao;
	pgt %0;
};
#alias tcmx.zhao
{
	zhao;
	#delay {1}
	{
		look;
	};
};
#alias tcmx.over
{
	start_tc;
	give cai wu to lin;
	#delay {2}
	{
		asktc.job;
	};
};


