#alias gthyd
{
	hgt 3ed5550ea65808a6fcbf7b0703c27a2d;
};
#var Nickname {阿诺};

#alias askfinder
{	
	#act {%*目前在【%*的%*】,快去摁死它吧!}
	{		
		#showme {finder get it!};
		#var finded 1;
		#var localdesc %%2%%3;	
		#unact {%*目前在【%*的%*】,快去摁死它吧!};
		stopt;
		#delay {2}
		{
			xia;
			#delay {3}
			{
				xia;
			};
			hu.t $localdesc 0;	
		};	
	};	
	helpme find ...;
	helpme find %0;
};
#alias start_hyd
{	
	#untick t_sk;			
	#class hyd open;	
	#alias askhyd.job
	{			
		ask hu about job;		
	};
	#act {^请注意，忽略验证码中的红色文字。}
	{
		#untick t_pf;
	};
	#alias kdbr
	{
		follow daobao ren;
		kill daobao ren;
	};
	#alias gtdbr
	{
		#var traversal_handler {stopTbyNpc Daobao ren};
		ptraversal %%0 0;		
	};
	#act {^盗宝人%*在%*的%*}
	{
		#var localdesc %%2%%3;		
		#delay {2}
		{
			#var traversal_handler {stopTbyNpc Daobao ren};
			#var tra {hu.t $localdesc 0};			    	
		    	#echo {$tra};
		    	$tra;	
		};	
	};
	#act {^胡一刀说道：『我收到消息，听说%*有盗宝人%*(%*)找到了闯王宝藏的地图,你可否帮忙找回来！』}
	{			
		#var localdesc %%1;			
		#format targetNpc_ename %l {%%3};
		#var targetNpc %%2;
		#var hererid {3ed5550ea65808a6fcbf7b0703c27a2d};
		#var finded 0;
		#var traversal_handler {stopTbyNpc_cn $targetNpc};
		#var tra {hu.t $localdesc};			    	
	    	#echo {$tra};
	    	$tra;	
		#delay {10}
		{			
			askfinder $targetNpc_ename;			
		};		
	};
	#alias askf
	{
		askfinder $targetNpc_ename;
	};	
	#act {^未有对手或者你和对方未处于战斗中}
	{		
		stophu;		
	};
	#act {^这里%*战斗。}
	{
		ask $targetNpc_ename about fight;
		#delay {1}
		{
			follow $targetNpc_ename;
			follow daobao ren;
		};
	};
	#alias fd
	{
		follow $targetNpc_ename;
		follow daobao ren;
	};
	#act {^你决定开始跟随%*一起行动。}
	{
		#unact {%*目前在【%*的%*】,快去摁死它吧!};
		stopt;			
		yun qi;
		#delay {1}
		{
			pfzong;
		};
		#delay {2}
		{
			yun taiji;		
		};
		kill $targetNpc_ename;
		kill daobao ren;
		#ticker t_pf {pfc} {0.2};
	};
	#act {说道：“你有种去%*找我兄弟%*(%*)，他会给我报仇的！”}
	{
		#untick t_pf;		
		#var localdesc %%1;			
		#format targetNpc_ename %l {%%3};
		#var targetNpc %%2;	
		#var finded 0;
		#var traversal_handler {stopTbyNpc_cn $targetNpc};
		#var tra {hu.t $localdesc};			    	
	    	#echo {$tra};
	    	$tra;	
		#delay {10}
		{			
			askfinder $targetNpc_ename;			
		};	
	};
	#act {长叹道：“人算不如天算，想不到我兄弟五人都栽在你的手中！”}
	{
		#untick t_pf;
		combine;
		#var destEventHandler {askhyd.over};
		set_destEventHandler $destEventHandler;
		gthyd;
	};
	#act {只能见招拆招，疲于应对，不觉已深陷太极剑意之中，无法出招。}
	{		
		#var pfl_count 0;
		#delay {2}
		{
			#ticker t_pfl 
			{
				pfl;
				#math pfl_count $pfl_count+1;
				#if {$pfl_count>6}
				{
					#untick t_pfl;
				};
			} {0.5};
		};
	};
	#alias askhyd.over
	{
		give cangbao tu to hu;
		#delay {2}
		{
			drop yun tie;
			drop huo tong;
			chakan bao tu;
		};
	};
	#alias xb
	{
		set_destEventHandler xunbao;
		pgt %%0;
	};
	#act {^你找到了闯王宝藏的线索}
	{	
		set_destEventHandler dazuo max;	
		gthyd;
	};
	#act {^你手上持有的武器并不能用来发动%*}
	{
		wield sword;
		wield sword 2;
		wield sword 3;
	};	
	#alias stophu
	{
		#untick t_pf;				
	};	
	
	#class hyd close;
};
#alias stop_hyd
{
	#var traversal_handler {};
	#class hyd kill;
};
#alias hu.t
{
	#act {^traversal_finished}
	{
		#unact {^traversal_finished};
		#delay {2}
		{
			r.hu.t 5;
		};
	};
	#act {^这里没有 %*。}
	{
		#nop only allow to go one step back;
		#unact {^这里没有 %*。};		
		#math istep_bak {$istep_bak-1};
		#var icmd @item{inner_Route_list;$istep_bak};
		$movemap[$icmd];
		#delay {1}
		{
			follow %%1;
		};
	};
	#var r_traversal {traversal %1};		
	#var locdesc %1;
	#var depth %2;
	#if {$depth==""}
	{
		#var depth 3;
	};
	#var traversal_flag 0;
	#script {ret} {python ./tintin/myscripts/pkuxkx/gps/getRidsByName.py "$locdesc"};
	#list ret {size} {retsize};	
	#if {"$retsize"=="0"}
	{
		#showme {can not find dest location!};
	}
	{
		#var torid $ret[1];
		#math hgtcount 1;
		hgt_pre $torid;	
	
		#act {^dest!}
		{
			#unact {^dest!};
			#showme {stopt to stop traversal.};
			$traversal_handler;
			#delay {1}
			{
				look;			
				#delay {1}
				{
					#if {$traversal_flag == 0}
					{	
						#script {ret} {python ./tintin/myscripts/pkuxkx/gps/getcmds_traversal.py "$locdesc" "$depth"};
						#list inner_Route_list create;
						#list inner_Route_list add {#showme {traversal_start};};
						#list inner_Route_list add {setSlowWalk2;};
						#list pre2dest size pre2dest_size;
						#if {$pre2dest_size>1}
						{
							#foreach {$pre2dest[%*]} {cmd} {#list inner_Route_list add {$cmd};};	
						};
						#foreach {$ret[%*]} {cmd} {#list inner_Route_list add {$cmd};};	
						#list inner_Route_list add {#showme {traversal_finished};#var traversal_flag 1;};
						#list inner_Route_list add {setNormalWalk;};					
						inner_walk;
	
					};
				};
			};
		};		
	};
};
#alias r.hu.t
{
	traversal $localdesc %1;
	#act {^这里没有 %*。}
	{
		#nop only allow to go one step back;
		#unact {^这里没有 %*。};		
		#math istep_bak {$istep_bak-1};
		#var icmd @item{inner_Route_list;$istep_bak};
		$movemap[$icmd];
		#delay {1}
		{
			follow %%1;
		};
	};	
};


